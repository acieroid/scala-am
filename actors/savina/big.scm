;; Adapted from Savina benchmarks ("Big" benchmark, from BenchErl)
(define num-messages (int-top))
(define num-workers (int-top))
(define (build-vector n f)
  (letrec ((v (make-vector n #f))
           (loop (lambda (i)
                   (if (< i n)
                       (begin
                         (vector-set! v i (f i))
                         (loop (+ i 1)))
                       v))))
    (loop 0)))
(define (vector-foreach f v)
  (letrec ((loop (lambda (i)
                   (if (< i (vector-length v))
                       (begin
                         (f (vector-ref v i))
                         (loop (+ i 1)))
                       'done))))
    (loop 0)))
(define big-actor
  (a/actor "big" (id num-pings sink exp-pinger neighbors)
           (ping (sender-id)
                 (a/send (vector-ref neighbors sender-id) pong id)
                 (a/become big-actor id num-pings sink exp-pinger neighbors))
           (pong (sender)
                 (if (= sender exp-pinger)
                     (if (= num-pings num-messages)
                         (begin
                           (a/send sink exit)
                           (a/become big-actor id num-pings sink exp-pinger neighbors))
                         (let ((target (random (vector-length neighbors))))
                           (a/send (vector-ref neighbors target) ping id)
                           (a/become big-actor id (+ num-pings 1) sink target neighbors)))
                     (error "wrong sender")))
           (exit () (a/terminate))
           (neighbors (new-neighbors) (a/become big-actor id num-pings sink exp-pinger new-neighbors))))
(define sink-actor
  (a/actor "sink" (num-messages neighbors)
           (exit ()
                 (if (= (+ num-messages 1) num-workers)
                     (begin
                       (vector-foreach (lambda (a) (a/send a exit)) neighbors)
                       (a/terminate))
                     (a/become sink-actor (+ num-messages 1) neighbors)))
           (neighbors (new-neighbors)
                      (a/become sink-actor num-messages new-neighbors))))
(define sink (a/create sink-actor 0 #f))
(define big-actors (build-vector num-workers (lambda (i) (a/create big-actor i 0 sink -1 #f))))
(a/send sink neighbors big-actors)
(vector-foreach (lambda (a) (a/send a neighbors big-actors)) big-actors)
(vector-foreach (lambda (a) (a/send a pong -1)) big-actors)
